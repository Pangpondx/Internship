#include <SPI.h>
#include <Ethernet.h>
#include <EthernetUdp.h>

// 1. สร้างโครงสร้างขนาด 8 บิต
struct GimbalStatus {
  unsigned int LS_ARM_EXT : 1; 
  unsigned int LS_INTL_EXT : 1; 
  unsigned int LS_ES_EXT : 1; 
  unsigned int LS_NES_EXT : 1; 
  unsigned int ANN_ARM_EXT : 1; 
  unsigned int ANN_FIRE_EXT : 1; 
  unsigned int VDC_PRT_EXT : 1; 
  unsigned int PWR_OFF_DSCR_EXT : 1; 
};

// ใช้ Union เพื่อให้เข้าถึงข้อมูลได้ทั้งแบบบิต (Struct) และแบบไบต์ (raw)
union DataPacket {
  GimbalStatus status;
  byte rawByte;
};

DataPacket myGimbal; 

byte mac[] = { 0xDE, 0xAD, 0xBE, 0xEF, 0xFE, 0xED };
IPAddress ip(192, 168, 200, 20);
IPAddress broadcastIp(192, 168, 200, 255);
unsigned int port = 8888;
EthernetUDP Udp;

unsigned long lastSendTime = 0;
const int interval = 200;

void setup() {
  Serial.begin(9600);
  Ethernet.begin(mac, ip);
  Udp.begin(port);

  myGimbal.rawByte = 0; // เริ่มต้นที่ 0 ทุกบิต

  Serial.println("--- Gimbal Control Ready ---");
  Serial.println("Press keys 0-7 to toggle bits:");
  Serial.println("Start Bit : 00000000");
}

void loop() {
  // รับค่าจากคีย์บอร์ด 
  if (Serial.available() > 0) {
    char key = Serial.read();
    
    // ตรวจสอบเลข 0-7
    if (key >= '0' && key <= '7') {
      int bitToToggle = key - '0'; // แปลงอักขระเป็นตัวเลข 0-7
      
      // ใช้ XOR เหมือน 0 ต่าง 1
      myGimbal.rawByte ^= (1 << bitToToggle);
      
      Serial.print("Toggled Bit: "); Serial.print(bitToToggle);
      Serial.print(" | Current Byte (BIN): "); Serial.println(myGimbal.rawByte, BIN);
    }
  }

  if (millis() - lastSendTime >= interval) {
    lastSendTime = millis();
    
    Udp.beginPacket(broadcastIp, port);
    Udp.write(myGimbal.rawByte);
    Udp.endPacket();
  }
}
